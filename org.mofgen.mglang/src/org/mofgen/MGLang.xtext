grammar org.mofgen.MGLang with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate mGLang "http://www.mofgen.org/MGLang"

// TODO Unique names for variable and pattern names (except for outer/inner scope shadowing) ?

MofgenFile:
	(imports+=Import)*
	(config=Config)
	(commands+=FileCommand)*;

FileCommand:
	Pattern | Generator;

Import:
	'import' uri=STRING 'as' name=ID;

Config:
	{Config} 'config' '(' ')' '{'
	(expressions+=ConfigExpression)*
	'}';

ConfigExpression:
	HomeExpression | UseProjectAsHomeExpression;

HomeExpression:
	'home' ASSIGNMENT_OP uri=STRING;

UseProjectAsHomeExpression:
	'useProjectLocationAsHome' ASSIGNMENT_OP val=BooleanLiteral;

Pattern:
	'pattern' name=ID ('(' (parameters+=Parameter (',' parameters+=Parameter)*)? ')') '{' (commands+=PatternCommand)* '}'
	(return=PatternReturn)?;

PatternCommand:
	Node | ParamManipulation; //TODO Introduce one more switch type: outside of all nodes to specify whether the nodes should be created at all

PatternReturn:
	{PatternReturn} 'return' 'this' ('.' returnValue=[Node])?;

ParamManipulation:
	param=[ParameterNodeOrPattern] content=NodeContent;

Node:
	(srcModel=[Import] '.')? type=[ecore::EClass] name=ID (createdBy=NodeContent | (ASSIGNMENT_OP
	createdBy=PatternCall))?;

NodeContent:
	{NodeContent} '{' (refsAssigns+=NodeReferenceOrAssignmentOrControlFlow)* '}';

PatternCall:
	called=[Pattern] '(' params=PatternCallParameters ')';

PatternCallParameters:
	{PatternCallParameters} (params+=ArithmeticExpression (',' params+=ArithmeticExpression)*)?;

NodeReferenceOrAssignmentOrControlFlow:
	PatternNodeReference | NodeAttributeAssignment | PatternSwitch;

PatternNodeReference:
	type=[ecore::EReference] '->' target=[Node];

NodeAttributeAssignment:
	target=[ecore::ENamedElement] ASSIGNMENT_OP value=ArithmeticExpression;

Parameter:
	PrimitiveParameter | ParameterNodeOrPattern;

PrimitiveParameter:
	type=PrimitiveType name=ID;

ParameterNodeOrPattern:
	(srcModel=[Import] '.')? type=[ecore::EObject] name=ID;	//EObject to allow for eClasses from metamodels or for patterns as a whole

PatternSwitch:
	PatternIfElseSwitch | PatternSwitchCase;

PatternIfElseSwitch:
	'switch' '{' (cases+=PatternWhenCase)+ (default=PatternDefault)? '}';

PatternDefault returns PatternCaseBody:
	'default' ':' PatternCaseBody;

PatternSwitchCase:
	'switch' '(' attribute=RefOrCall ')' '{' (cases+=PatternCase)+ (default=PatternDefault)? '}';

PatternWhenCase:
	'when' when=ArithmeticExpression ':' body=PatternCaseBody;

PatternCase:
	PatternCaseWithCast | PatternCaseWithoutCast;

PatternCaseWithCast:
	'case' node=Node ('when' when=ArithmeticExpression)? ':' body=PatternCaseBody;

PatternCaseWithoutCast:
	'case' val=ArithmeticExpression':' body=PatternCaseBody;

PatternCaseBody:	//TODO: one more pattern switch object for switches outside of node definitions
	{PatternCaseBody} '{' (expressions+=NodeReferenceOrAssignmentOrControlFlow)* '}' | expressions+=NodeReferenceOrAssignmentOrControlFlow;

Case:
	PatternCase | GenCase;

	//Using generic eobject references to make enums possible.
RefOrCall:
	ref=[ecore::EObject] ({RefOrCall.target=current} '.' ref=[ecore::ENamedElement] ('(' (params=RefParams)?
	bracesSet?=')')?)*;
RefParams:
	params+=ArithmeticExpression (',' params+=ArithmeticExpression)*;

Generator:
	'gen' name=ID '(' (params+=Parameter)* ')' '{' (commands+=GeneratorExpression)* '}';

GeneratorExpression: //Only serves as super class for all possible commands within generator
	ForStatement | GenSwitch | Collection | Variable | VariableManipulation |
	CollectionManipulation | PatternCall | GenReturn
	;
	
GenReturn:
	'return' returnValue = [Variable] //TODO: validate 1. no code after return 2. Only valid: VariableRef, PatternCall,
;

CollectionManipulation:
	trg=[Collection] '.' op=[ecore::EOperation] '(' (params=RefParams)? ')';

VariableManipulation:
	var=[Variable] ASSIGNMENT_OP val=ArithmeticExpression;

Variable:
	'var' name=ID ASSIGNMENT_OP value=ArithmeticExpression;

ForStatement:
	'for' head=ForHead '{' body=ForBody '}';

ForHead:
	RangeForHead | GeneralForEachHead | ListForEachHead;

RangeForHead:
	iteratorVar=ID 'in' range=ForRange;

ForRange:
	start=ArithmeticExpression ':' end=ArithmeticExpression;

GeneralForEachHead:
	src=RefOrCall '-' eref=[ecore::EReference] '->' iteratorVar=IteratorVariable;

ListForEachHead:
	iteratorVar=IteratorVariable 'in' list=[List];

IteratorVariable:
	name=ID;

ForBody:
	{ForBody} (commands+=GeneratorExpression)*;

GenSwitchExpression:
	ForStatement | Collection | PatternCall;

GenSwitch:
	GenIfElseSwitch | GenSwitchCase;

GenIfElseSwitch:
	'switch' '{' (cases+=GenWhenCase)+ (default=GenDefault)? '}';

GenWhenCase:
	'when' when=ArithmeticExpression ':' body=GenCaseBody;

GenDefault:
	'default' ':' GenCaseBody;

GenSwitchCase:
	'switch' '(' attribute=RefOrCall ')' '{' (cases+=GenCase)+ (default=GenDefault)? '}';

GenCase:
	GenCaseWithCast | GenCaseWithoutCast;

GenCaseWithCast:
	'case' node=Node ('when' when=ArithmeticExpression)? ':' body=GenCaseBody;

GenCaseWithoutCast:
	'case' val=ArithmeticExpression ':' body=GenCaseBody;

GenCaseBody:
	{GenCaseBody} '{' (expressions+=GenSwitchExpression)* '}' | expressions+=GenSwitchExpression;

Collection:
	List | Map;

List:
	'List' name=ID (defOrDecl=ListDefOrDecl)?;

ListDefOrDecl:
	ListDefinition | ListDeclaration;

ListDeclaration:
	'of' type=[ecore::EClass];

ListDefinition:
	ASSIGNMENT_OP ListAdHoc;

ListAdHoc:
	{ListAdHoc} '[' (elements+=ArithmeticExpression (',' elements+=ArithmeticExpression)*)? ']';

Map:
	'Map' name=ID (defOrDecl=MapDefOrDecl)?;

MapDefOrDecl:
	MapDefinition | MapDeclaration;

MapDeclaration:
	'of' '(' keyType=[ecore::EClass] ',' entryType=[ecore::EClass] ')'
;

MapDefinition:
	ASSIGNMENT_OP MapAdHoc;

MapAdHoc:
	{MapAdHoc} '[' (entries+=MapTupel (',' entries+=MapTupel)*)? ']';

MapTupel:
	'(' key=ArithmeticExpression ',' value=ArithmeticExpression ')';

	//======================================Arithmetics============================================
	
	// TODO allow null checks
	
ArithmeticExpression:
	TertiaryExpression;

TertiaryExpression returns ArithmeticExpression:
	SecondaryExpression ({Tertiary.left=current} op=TertiaryOp right=SecondaryExpression)*;

SecondaryExpression returns ArithmeticExpression:
	PrimaryExpr ({Secondary.left=current} op=SecondaryOp right=PrimaryExpr)*;

PrimaryExpr returns ArithmeticExpression:
	RelationExpression ({Primary.left=current} op=PrimaryOp right=RelationExpression)*;

RelationExpression returns ArithmeticExpression:
	BaseExpr ({Rel.left=current} relation=RelationalOp right=BaseExpr)*;

BaseExpr returns ArithmeticExpression:
	'(' ArithmeticExpression ')' | {NegationExpression} '!' expr=BaseExpr | {FunctionCall} func=MathFunc '('
	expr=ArithmeticExpression ')' | {UnaryMinus} '-' expr=BaseExpr | Literal | RefOrCall | PatternCall;

Literal:
	BooleanLiteral | StringLiteral | NumberLiteral;

BooleanLiteral:
	val=(TRUE | FALSE);

StringLiteral:
	val=STRING;

NumberLiteral:
	val=DOUBLE;

enum MathFunc: //TODO Replace by methods of java.math or similar
	SQRT='sqrt' | ABS='abs';

enum PrimitiveType:	//TODO: Replace by standard eDataTypes?
	INT='int' | CHAR='char' | DOUBLE='double' | STRING='String' | BOOLEAN='boolean' //String as Primitive? (and therefore also better lowercase?)
;

terminal DOUBLE returns ecore::EDouble:
	INT ('.' INT)?;

terminal TRUE returns ecore::EBoolean:
	'true';

terminal FALSE returns ecore::EBoolean:
	'false';

enum RelationalOp:
	GREATER='>' |
	GREATER_OR_EQUAL='>=' |
	EQUAL='==' |
	UNEQUAL='!=' |
	LESS_OR_EQUAL='<=' |
	LESS='<';

enum TertiaryOp:
	PLUS='+' |
	MINUS='-' |
	OR='||';

enum SecondaryOp:
	MOD='%' |
	XOR='^';

enum PrimaryOp:
	MUL='*' |
	DIV='/' |
	AND='&&';

terminal ASSIGNMENT_OP:
	'=';