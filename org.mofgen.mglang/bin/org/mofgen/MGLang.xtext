grammar org.mofgen.MGLang with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate mGLang "http://www.mofgen.org/MGLang"

//TODO: Variables
MofgenFile:
	(imports+=Import)*
	(config=Config)
	(patterns+=Pattern |
	generators+=Generator)*;

Import:
	'import' uri=STRING 'as' name=ID;

Config:
	{Config} 'config' '(' ')' '{'
	(expressions += ConfigExpression)*
	'}'
	;

ConfigExpression:
	'test'
;

Pattern:
	'pattern' name=ID ('(' (parameters+=Parameter (',' parameters+=Parameter)*)? ')') '{' (((colls+=Collection)*)? &
	((nodes+=Node)*)? & ((paramManipulations+=ParamManipulation)*)? & ((switches+=Switch)*)?) '}'
	(return=PatternReturn)?;

PatternReturn:
	{PatternReturn} 'return' 'this' ('.' returnValue=[Node])?;

ParamManipulation:
	param=[ParameterNode] content=NodeContent;

Node:
	type=[ecore::EClass] name=ID (createdBy=NodeContent | (ASSIGNMENT_OP createdBy=PatternCall))?;

NodeContent:
	{NodeContent} '{' (refsAssigns+=NodeReferenceOrAssignmentOrControlFlow)* '}';

PatternCall:
	called=[Pattern] '(' (params+= ArithmeticExpression (',' params+=ArithmeticExpression)*)? ')';

NodeReferenceOrAssignmentOrControlFlow:
	(PatternNodeReference | Assignment | Switch);

PatternNodeReference:
	type=[ecore::EReference] '->' target=[Node];

Assignment:
	target=[ecore::EAttribute] ASSIGNMENT_OP value=ArithmeticExpression;
	//TODO make Enum Assignments possible
Parameter:
	PrimitiveParameter | ParameterNode;

PrimitiveParameter:
	type=PrimitiveType name=ID;

ParameterNode:
	(srcModel=[Import]'.')? type=[ecore::EClassifier] name=ID;	//TODO: Handling if no srcModel is given? And show warning!

RefOrCall:
	ref=[RefType] ({RefOrCall.target=current} '.' ref=[ecore::ETypedElement] ('(' (params = RefParams)? ')')?)*;

RefParams:
	params += ArithmeticExpression (',' params+=ArithmeticExpression)*;

Generator:
	{Generator} 'gen' '(' (params+=Parameter)* ')' '{' (commands+=GeneratorExpression)* '}';

GeneratorExpression: //Only serves as super class for all possible commands within generator
	ForStatement | Switch | Collection | PatternCall | PatternVariable;

PatternVariable:
	type=[Pattern] name=ID ASSIGNMENT_OP patternCall=PatternCall;

ForStatement:
	'for' head=ForHead '{' body=ForBody '}';

ForHead:
	GeneralForHead | ForEachHead;

GeneralForHead:
	iteratorVar=ID 'in' range=ForRange;

ForEachHead:
	src=RefOrCall '-' eref=[ecore::EReference] '->' iteratorVar=ID;

RefType:
	Node | Parameter | Collection;

ForBody:
	{ForBody} (commands+=GeneratorExpression)*;

SwitchExpression:
	ForStatement | Collection | PatternCall | PatternVariable | NodeReferenceOrAssignmentOrControlFlow
;

Switch:
	IfElseSwitch | SwitchCase;

IfElseSwitch:
	'switch' '{' (cases+=IfElseCase)+ (default=Default)? '}';

IfElseCase:
	'case' val=ArithmeticExpression ':' body=CaseBody;

Default:
	'default' ':' CaseBody;

SwitchCase:
	'switch' '(' attribute=RefOrCall ')' '{' (cases+=Case)+ (default=Default)? '}';

Case:
	CaseWithCast | CaseWithoutCast;

CaseWithCast:
	// TODO: Check that arithmetic expression really yields boolean value
	'case' node=Node ('when' when=ArithmeticExpression)? ':' body=CaseBody;

CaseWithoutCast:
	'case' val=ArithmeticExpression ':' body=CaseBody;

CaseBody:
	{CaseBody} '{' (expressions+=SwitchExpression)* '}' | (expressions+=SwitchExpression NEWLINE);

Collection:
	List | Map;

List:
	'List' name=ID (ASSIGNMENT_OP createdBy=ListAssignment)?;

ListAssignment:
	ListAdHoc | RefOrCall;

ListAdHoc:
	'[' elements+=Literal (',' elements+=Literal)* ']' //TODO Allow empty lists? --> Probably have no purpose here since one will not be able to manipulate them, right?
;

Map:
	'Map' name=ID (ASSIGNMENT_OP createdBy=MapAssignment)?;

MapAssignment:
	MapAdHoc | RefOrCall;	

MapAdHoc:
	'[' entries+=MapTupel (',' entries+=MapTupel)* ']';

MapTupel:
	'(' key=Literal ',' value=ArithmeticExpression ')';

ForRange:
	start=ArithmeticExpression ':' end=ArithmeticExpression;

	//======================================Arithmetics============================================
ArithmeticExpression:
	TertiaryExpression;

TertiaryExpression returns ArithmeticExpression:
	SecondaryExpression ({Tertiary.left=current} op=TertiaryOp right=SecondaryExpression)*;

SecondaryExpression returns ArithmeticExpression:
	PrimaryExpr ({Secondary.left=current} op=SecondaryOp right=PrimaryExpr)*;

PrimaryExpr returns ArithmeticExpression:
	RelationExpression ({Primary.left=current} op=PrimaryOp right=RelationExpression)*;

RelationExpression returns ArithmeticExpression:
	BaseExpr ({Rel.left=current} relation=RelationalOp right=BaseExpr)*;

BaseExpr returns ArithmeticExpression:
	'(' ArithmeticExpression ')' | {NegationExpression} '!' expr=BaseExpr | {FunctionCall} func=MathFunc '('
	expr=ArithmeticExpression ')' | Literal | RefOrCall;

Literal:
	BooleanLiteral | StringLiteral | NumberLiteral;

BooleanLiteral:
	val = (TRUE|FALSE)
;

StringLiteral:
	val = STRING;

NumberLiteral:
	val = DOUBLE; //TODO: Negative Numbers?
	
enum MathFunc:
	SQRT='sqrt' | ABS='abs';

enum PrimitiveType:
	INT='int' | CHAR='char' | DOUBLE='double' | STRING='String' //TODO: String as Primitive? (and therefore also better lowercase?)
;

terminal DOUBLE returns ecore::EDouble:
	INT ('.' INT)?
;

terminal TRUE returns ecore::EBoolean:
	'true';

terminal FALSE returns ecore::EBoolean:
	'false';

enum RelationalOp:
	GREATER='>' |
	GREATER_OR_EQUAL='>=' |
	EQUAL='==' |
	UNEQUAL='!=' |
	LESS_OR_EQUAL='<=' |
	LESS ='<';

enum TertiaryOp:
	PLUS='+' |
	MINUS='-' |
	OR='||';

enum SecondaryOp:
	MOD='%' |
	XOR='^';

enum PrimaryOp:
	MUL='*' |
	DIV='/' |
	AND='&&';

terminal ASSIGNMENT_OP:
	'=';

terminal NEWLINE:
	'\n';
